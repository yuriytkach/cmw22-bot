plugins {
    id 'java-library'
    id 'io.quarkus'
    id 'io.freefair.lombok' version '8.6'
    id 'jacoco'
    id 'org.kordamp.gradle.jandex' version "1.1.0"
}

dependencies {
    api 'io.quarkus:quarkus-arc'
    api 'io.quarkus:quarkus-config-yaml'
    api 'io.quarkus:quarkus-rest-client'
    api 'io.quarkus:quarkus-amazon-lambda'
    api 'io.quarkus:quarkus-cache'
    api 'io.quarkiverse.amazonservices:quarkus-amazon-ssm'

    api 'software.amazon.awssdk:url-connection-client'

    api 'one.util:streamex:0.8.2'
    api 'com.github.spotbugs:spotbugs-annotations:4.8.3'
}

tasks.withType(Test).configureEach {
    dependsOn 'jandex'
    finalizedBy jacocoTestReport
    systemProperty "java.util.logging.manager", "org.jboss.logmanager.LogManager"
    testLogging {
        events "passed", "skipped", "failed"
    }
    jacoco {
        excludeClassLoaders = ["*QuarkusClassLoader"]
        destinationFile = layout.buildDirectory.file("jacoco-quarkus.exec").get().asFile
    }
}

jacocoTestReport {
    dependsOn test
    executionData { tasks.withType(Test).findAll { it.jacoco.destinationFile.exists() }*.jacoco.destinationFile }
    reports {
        xml.required = true
    }
}

tasks.named('compileTestJava') {
    dependsOn 'jandex'
}
